# Tests for the 'DiffUtil' package. -*- tcl -*-
#
# Copyright (c) 2004 by Peter Spjuth. All rights reserved.
#
# $Revision: 1.3 $

package require DiffUtil

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::*
}

#----------------------------------------------------------------------

# A wrapper to simplify calling
proc RunTest {list1 list2 args} {
    set ch [open _diff_1 w]
    if {[llength $list1] > 0} {
        puts $ch [join $list1 \n]
    }
    close $ch
    set ch [open _diff_2 w]
    if {[llength $list2] > 0} {
        puts $ch [join $list2 \n]
    }
    close $ch

    set apa [catch {eval DiffUtil::diffFiles $args _diff_1 _diff_2} res]
    file delete -force _diff_1 _diff_2
    return $res
}

#----------------------------------------------------------------------

test difffiles-1.1 {standard cases} {
    set l1 {a b c d   f g h i j k l}
    set l2 {  b c d e f g x y   k l}
    RunTest $l1 $l2
} [list {1 1 1 0} {5 0 4 1} {7 3 7 2}]

test difffiles-1.2 {standard cases, error} -body {
    set l1 {a b c d   f g h i j k l}
    set l2 {  b c d e f g x y   k l}
    RunTest $l1 $l2 -hubba
} -result {bad option "-hubba"*} -match glob

test difffiles-1.3 {standard cases, error} -body {
    DiffUtil::diffFiles a
} -returnCodes 1 -result "wrong # args*" -match glob

test difffiles-1.4 {standard cases, error} -body {
    DiffUtil::diffFiles -range {0 0 0 0} -align {0 0} a b
} -returnCodes 1 -result "Both -align and -range are not currently supported"

test difffiles-2.1 {alignment, no align} {
    set l1 {a b c d  }
    set l2 {a   c d b}
    RunTest $l1 $l2
} [list {2 1 2 0} {5 0 4 1}]

test difffiles-2.2 {alignment} {
    set l1 {a     b c d}
    set l2 {a c d b    }
    RunTest $l1 $l2 -align {2 4}
} [list {2 0 2 2} {3 2 5 0}]

test difffiles-2.3 {alignment, impossible align} {
    set l1 {a     b c d}
    set l2 {a c d b    }
    RunTest $l1 $l2 -align {2 4 3 2}
} [list {2 0 2 2} {3 1 5 0} {4 1 5 0}]


test difffiles-3.1 {ignore space} {
    set l1 {a {b c} d e f {g h}  i}
    set l2 {a {bc}  x e f {g  h} i}
    RunTest $l1 $l2
} [list {2 2 2 2} {6 1 6 1}]

test difffiles-3.2 {ignore space} {
    set l1 {a {b c} d e f {g h}  i}
    set l2 {a {bc}  x e f {g  h} i}
    RunTest $l1 $l2 -b
} [list {2 2 2 2}]

test difffiles-3.3 {ignore space} {
    set l1 {a {b c} d e f {g h}  i}
    set l2 {a {bc}  x e f {g  h} i}
    RunTest $l1 $l2 -w
} [list {3 1 3 1}]


test difffiles-4.1 {range} {
    set l1 {a bc d e f gh  i}
    set l2 {a xy x e f ghh i}
    RunTest $l1 $l2 -range {3 5 3 6}
} [list {3 1 3 1} {6 0 6 1}]

test difffiles-4.2 {range} -body {
    set l1 {a bc d e f gh  i}
    set l2 {a xy x e f ghh i}
    RunTest $l1 $l2 -range {3 5 3}
} -result {Bad range*} -match glob

#----------------------------------------------------------------------
::tcltest::cleanupTests
